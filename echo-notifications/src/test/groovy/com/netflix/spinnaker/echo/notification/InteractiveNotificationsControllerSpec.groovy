/*
 * Copyright 2020 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package com.netflix.spinnaker.echo.notification

import com.jakewharton.retrofit.Ok3Client
import com.netflix.spinnaker.echo.api.Notification
import com.netflix.spinnaker.echo.controller.InteractiveNotificationsController
import com.netflix.spinnaker.kork.web.exceptions.InvalidRequestException
import org.springframework.core.env.Environment
import org.springframework.http.HttpHeaders
import org.springframework.http.HttpMethod
import org.springframework.http.HttpStatus
import org.springframework.http.RequestEntity
import org.springframework.http.ResponseEntity
import retrofit.client.Response
import retrofit.mime.TypedByteArray
import spock.lang.Specification
import spock.lang.Subject

import static java.util.Collections.emptyList

class InteractiveNotificationsControllerSpec extends Specification {
  static final String CONTROLLER_URI = "/interactive-notifications/callbacks"
  InteractiveNotificationCallbackHandler.SpinnakerService spinnakerService
  InteractiveNotificationService notificationService
  InteractiveNotificationCallbackHandler callbackHandler

  @Subject
  InteractiveNotificationsController interactiveNotificationsController

  void setup() {
    notificationService = Mock()
    spinnakerService = Mock()
    callbackHandler = new InteractiveNotificationCallbackHandler(
      [ notificationService ],
      [ "test": spinnakerService ],
      Mock(Environment)
    )
    interactiveNotificationsController = new InteractiveNotificationsController(
      notificationServices: [ notificationService ],
      callbackHandler: callbackHandler
    )
  }

  void 'an incoming callback from the notification service delegates to the appropriate service class'() {
    given:
    RequestEntity<String> request = new RequestEntity<>(
      "blah", new HttpHeaders(), HttpMethod.POST, new URI(CONTROLLER_URI))

    Notification.InteractiveActionCallback callbackObject = new Notification.InteractiveActionCallback()
    callbackObject.serviceId = "test"
    callbackObject.user = "john.doe"

    notificationService.supportsType(Notification.Type.SLACK) >> true
    spinnakerService.notificationCallback(*_) >> { mockResponse() }

    when:
    interactiveNotificationsController.processCallback("slack", request)

    then:
    1 * notificationService.parseInteractionCallback(request) >> callbackObject
    1 * notificationService.respondToCallback(request) >> { Optional.empty() }
  }

  void 'handling of the incoming callback throws an exception if Spinnaker service config not found'() {
    given:
    RequestEntity<String> request = new RequestEntity<>(
      "blah", new HttpHeaders(), HttpMethod.POST, new URI(CONTROLLER_URI))

    Notification.InteractiveActionCallback callbackObject = new Notification.InteractiveActionCallback()
    callbackObject.serviceId = "unknown"
    callbackObject.user = "john.doe"

    notificationService.supportsType(Notification.Type.SLACK) >> true
    notificationService.parseInteractionCallback(request) >> callbackObject

    when:
    interactiveNotificationsController.processCallback("slack", request)

    then:
    thrown(InvalidRequestException)
  }

  void 'handling of the incoming callback returns the response generated by the corresponding service class'() {
    given:
    RequestEntity<String> request = new RequestEntity<>(
      "blah", new HttpHeaders(), HttpMethod.POST, new URI(CONTROLLER_URI))

    ResponseEntity<String> expectedResponse = new ResponseEntity(HttpStatus.OK)

    Notification.InteractiveActionCallback callbackObject = new Notification.InteractiveActionCallback()
    callbackObject.serviceId = "test"
    callbackObject.user = "john.doe"

    notificationService.supportsType(Notification.Type.SLACK) >> true
    spinnakerService.notificationCallback(*_) >> { mockResponse() }

    when:
    ResponseEntity<String> response = interactiveNotificationsController.processCallback("slack", request)

    then:
    1 * notificationService.parseInteractionCallback(request) >> callbackObject
    1 * notificationService.respondToCallback(request) >> { Optional.of(expectedResponse) }
    response == expectedResponse
  }

  static Response mockResponse() {
    new Response("url", 200, "nothing", emptyList(), new TypedByteArray("application/json", "response".bytes))
  }
}
